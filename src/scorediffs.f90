SUBROUTINE scorediffs(exp1,exp2,nfclen,par,     &
                      lnorm,lztrans,confide,    &
                      diff,ncases)

 USE data, ONLY: use_fclen
 USE sign_data 

 IMPLICIT NONE 

 ! Input
 INTEGER, INTENT(IN) :: exp1,exp2
 INTEGER, INTENT(IN) :: nfclen,par
 LOGICAL, INTENT(IN) :: lnorm,lztrans
 REAL,    INTENT(IN) :: confide

 ! Output

 REAL,    INTENT(OUT)   :: diff(nfclen,2)
 INTEGER, INTENT(INOUT) :: ncases(nfclen)

 ! Local
 INTEGER :: i,j,l

 REAL :: x(sign_stat_max),y(sign_stat_max),d(sign_stat_max,nfclen)

 REAL ::  avs,avd,sigmean,dy,tcrit,confid
     
 EXTERNAL :: t4_confid
 REAL     :: t4_confid


!--- header comment for output
      confid = confide/100.0

  DO j=1,nfclen

      l = 0
      ncases(j) = 0
      DO i=1,sign_stat_max
        IF ( all_sign_stat(i)%n(1,j,par) == 0 ) CYCLE 
        l = l + 1
        x(l) = SQRT( all_sign_stat(i)%r(exp1,j,par) /  &
                       all_sign_stat(i)%n(1,j,par) )
        y(l) = SQRT( all_sign_stat(i)%r(exp2,j,par) /  &
                       all_sign_stat(i)%n(1,j,par) )

        ncases(j) = ncases(j) + all_sign_stat(i)%n(1,j,par)

      ENDDO 

!--- transform correlations to something more Gaussian

      if (lztrans) then
        x(1:l) = 0.5*log( (1.-0.01*x(1:l))/(1.+0.01*x(1:l)) )
        y(1:l) = 0.5*log( (1.-0.01*y(1:l))/(1.+0.01*y(1:l)) )
      endif

!--- calculate the value of t corresponding to the confidence interval
!--- using bisection.

       tcrit= t4_confid(confid,l-1) 

!--- calculate differences

     d(:,j) = x(:)-y(:)

!--- mean scores and mean difference

     avs=0.
     avd=0.
     do i=1,l
       avs = avs + x(i) + y(i)
       avd = avd + d(i,j)
     enddo
     avs =0.5*avs/l
     avd =avd/l

!---- normalize

      if (lnorm) then
          d(1:l,j)  = d(1:l,j)/avs
          avd = avd/avs
      endif

!--- auto-correlation-corrected standard deviation of mean

      call stats (d(1:l,j),avd,l,sigmean)

!--- confidence interval is between avd-dy and avd+dy

      dy = sigmean*tcrit

      diff(j,:) = (/avd,dy/)

   ENDDO

END SUBROUTINE scorediffs

SUBROUTINE stats (d,avd,nsc,sigmean)

!--- returns an estimate of the population standard deviation of the
!--- mean of 'd', where 'd' is supposed to be a sample generated by a
!--- first-order auto-regressive process. (i.e. d(i) =r*d(i-1)+e(i)
!--- for a fixed coefficient r and independent random variables e(i).)

      implicit none

      integer nsc,i,j

      real d(nsc)

      real avd,sigmean,var,covd1,cord,rtoj,rinflt,std
       

!--- Sample variance

      var = 0.
      do i=1,nsc
       var = var + (d(i)-avd)*(d(i)-avd)
      enddo
      var = var/nsc

!--- Sample lag-one auto-covariance

      covd1 = 0.
      do i=2,nsc
       covd1 = covd1 + (d(i)-avd)*(d(i-1)-avd)
      enddo
      covd1 = covd1/nsc

!--- lag-one auto-correlation

      cord = covd1/var

!--- variance and stdev of sample mean neglecting auto-correlation

      var = var/(nsc-1)
      std = sqrt(var)

!--- inflation factor to account for auto-correlation:
!---     rinflt = sqrt( (1/N) sum(cord**abs(i-j)) )
!--- where the sum is over i=1,...,nsc and j=1,...,nsc

      rtoj = 1.0
      rinflt = nsc

      do j=1,nsc-1
       rtoj = rtoj*cord
       rinflt = rinflt + 2*(nsc-j)*rtoj
      enddo
      rinflt = max(1.0,sqrt(rinflt/nsc))
!      write (0,*) 'lag-one auto-corr=',cord,
!     &            ' stdev of mean inflated by ',rinflt

!--- auto-correlation corrected standard deviation

      sigmean = std*rinflt

 RETURN
END SUBROUTINE stats

! ---- Get t value from the t-test table
      real function t4_confid(x,df)
      real x
      integer df
      if( ABS(x-0.90) < 1.e-6 ) then
         select case(df)
             case(1)
                t4_confid=6.314
             case(2)
                t4_confid=2.920
             case(3)
                t4_confid=2.353
             case(4)
                t4_confid=2.132
             case(5)
                t4_confid=2.015
             case(6)
                t4_confid=1.943
             case(7)
                t4_confid=1.895
             case(8)
                t4_confid=1.860
             case(9)
                t4_confid=1.833
             case(10)
                t4_confid=1.812
             case(11)
                t4_confid=1.797
             case(12)
                t4_confid=1.782
             case(13)
                t4_confid=1.772
             case(14)
                t4_confid=1.761
             case(15)
                t4_confid=1.754
             case(16)
                t4_confid=1.746
             case(17)
                t4_confid=1.740
             case(18)
                t4_confid=1.734
             case(19)
                t4_confid=1.730
             case(20)
                t4_confid=1.725
             case(21)
                t4_confid=1.721
             case(22)
                t4_confid=1.717
             case(23)
                t4_confid=1.714
             case(24)
                t4_confid=1.711
             case(25)
                t4_confid=1.708
             case(26)
                t4_confid=1.706
             case(27)
                t4_confid=1.704
             case(28)
                t4_confid=1.701
             case(29)
                t4_confid=1.699
             case(30)
                t4_confid=1.694
             case default
                t4_confid=1.645
         end select
       else
         write(6,*)'Cannot handle confidence interval',x
         call abort
       endif
      return
      end
